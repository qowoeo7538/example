buildscript {
    repositories {
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("io.spring.gradle:propdeps-plugin:0.0.8")
    }
}

plugins {
    id "com.gradle.build-scan" version "1.11"
    id "io.spring.dependency-management" version "1.0.4.RELEASE" apply false
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

ext {
    moduleProjects = subprojects
}

configure(allprojects) { project ->

    apply plugin: "propdeps"
    apply plugin: "java"

    group = 'org.shaw'
    version = '1.0-SNAPSHOT'

    ext {
        springVersion = "5.0.1.RELEASE"
        javassistVersion = "3.22.0-GA"
        junitVersion = "4.12"
        mockitoVersion = "2.12.0"
        hamcrestVersion = "1.3"
        servletVersion = "3.1.0"
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

configure(moduleProjects) { subproject ->

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework:spring-framework-bom:${springVersion}"
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
        applyMavenExclusions = false
    }

    [compileJava, compileTestJava].each() {
        it.options.encoding = 'UTF-8'
        it.sourceCompatibility = 11
        it.targetCompatibility = 11
    }

    compileTestJava {
        options.compilerArgs += "-parameters"
    }

    dependencies {
        compileOnly("junit:junit:${junitVersion}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        compileOnly("org.hamcrest:hamcrest-all:${hamcrestVersion}")
        compileOnly("com.google.code.findbugs:jsr305:3.0.2")
        testCompile("org.mockito:mockito-core:${mockitoVersion}") {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }

    }
}

configure(rootProject) {

    description = "Example Project"

    configurations.archives.artifacts.clear()
}